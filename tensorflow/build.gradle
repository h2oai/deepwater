group 'deepwater.backends.tensorflow'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'maven'

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs "${rootDir}/tensorflow/src/main/grpc"
        }
    }
}

dependencies {

    def presets = "${rootDir}/thirdparty/javacpp-presets"

    compile files("${presets}/target/javacpp.jar")
    compile files("${presets}/tensorflow/target/tensorflow.jar")

    if (new File("${presets}/tensorflow/target/tensorflow-linux-x86_64.jar").exists()) {
        compile files("${presets}/tensorflow/target/tensorflow-linux-x86_64.jar")
    }
    else if (new File("${presets}/tensorflow/target/tensorflow-macosx-x86_64.jar").exists()) {
        compile files("${presets}/tensorflow/target/tensorflow-macosx-x86_64.jar")
    }

    compile project (':deepwater-backend-api')
    compile "com.google.protobuf:protobuf-java:3.1.0"
    compile 'com.google.guava:guava:19.0'

    compile 'io.grpc:grpc-netty:1.0.1'
    compile 'io.grpc:grpc-protobuf:1.0.1'
    compile 'io.grpc:grpc-stub:1.0.1'

    // https://mvnrepository.com/artifact/org.hdfgroup/hdf-java
    compile group: 'org.hdfgroup', name: 'hdf-java', version: '2.6.1'

    // https://mvnrepository.com/artifact/com.google.code.gson/gson
    compile group: 'com.google.code.gson', name: 'gson', version: '2.7'

}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
}


jar {
    baseName = rootProject.name + "." + project.name

    // include also the sources for easy debugging
    from sourceSets.main.allSource

    // copy all the other dependencies as a single JAR
    from configurations.compile.collect {it.isDirectory() ? it: zipTree(it)}
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

